//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/// Generic logger
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_DIMORPHIC_FUNCTION "LOG_THIS"
STR_VAR
	"file" = ""	// With extension â€“ for instance: "test.txt"
	"input" = ""
	"repeat" = "yes"
	"location" = ""
BEGIN
	ACTION_IF ((~%file%~ STRING_EQUAL_CASE ~~) OR (~%input%~ STRING_EQUAL_CASE ~~) OR (~%location%~ STRING_EQUAL_CASE ~~)) BEGIN
		FAIL ~LOG_THIS: "file", "input" and "location" cannot be empty!~
	END
	ACTION_IF ("%debug_variable%" == 2) BEGIN
		PRINT ~Trying to append "%input%" to "%file%"; location defined by "%location%"~
	END
	ACTION_IF !(FILE_EXISTS ~%location%/%file%~) BEGIN
		COPY ~.../pnp_potions-inline/blank~ ~%location%/%file%~
			DELETE_BYTES 0x0 BUFFER_LENGTH
		BUT_ONLY_IF_IT_CHANGES
	END
	ACTION_IF ((~%repeat%~ STRING_COMPARE_CASE ~no~) || !(FILE_CONTAINS_EVALUATED ("%location%/%file%" "%input%\($\|%WNL%\|%MNL%\|%LNL%\)"))) BEGIN
		APPEND_OUTER ~%location%/%file%~ ~%input%~
	END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	General warning message
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_DIMORPHIC_FUNCTION "WARNING"
STR_VAR
	"warning" = ""
	"file" = "lfo_warnings.txt"
	"location" = "%warning_loc%"
	"repeat" = "no"
BEGIN
	LAF "LOG_THIS"
	STR_VAR
		"file"
		"input" = ~~~~~"%TP2_BASE_NAME%": component #%COMPONENT_NUMBER%, language "%LANGUAGE%", WeiDU OS "%WEIDU_OS%", WeiDU architecture "%WEIDU_ARCH%" => %warning%~~~~~
		"location"
		"repeat"
	END
	WARN ~~~~~%warning%~~~~~
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//	Include a string as a line of code
//	In so doing, you can treat "spellLevel" of "ADD_MEMORIZED_SPELL" as a variable (i.e., #%spellLevel% becomes valid syntax)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION "REINCLUDE_THIS"
STR_VAR
	"input" = ""
RET
	"output"
BEGIN
	TEXT_SPRINT "input" "output = %input%"
	INNER_ACTION BEGIN
		COPY - ".../pnp_potions-inline/blank" "override/temp_file.tpp"
			DELETE_BYTES 0x0 BUFFER_LENGTH
			INSERT_BYTES 0x0 (STRING_LENGTH "%input%")
			WRITE_ASCIIE 0x0 "%input%"
		BUT_ONLY_IF_IT_CHANGES
	END
	PATCH_REINCLUDE "override/temp_file.tpp"
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////
///// Make and check markers stored in marker_loc
//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// Make a label
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION "MAKE_LABEL"
STR_VAR
	"label" = ""
BEGIN
	COPY ~.../pnp_potions-inline/blank~ ~%marker_loc%/%label%.mrk~
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// Check a label
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_DIMORPHIC_FUNCTION "CHECK_LABEL"
STR_VAR
	"label" = ""
RET
	"value"
BEGIN
	ACTION_IF (FILE_EXISTS ~%marker_loc%/%label%.mrk~) BEGIN
		OUTER_SET "value" = 1
	END ELSE BEGIN
		OUTER_SET "value" = 0
	END
END

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Include and run a program (.tph), if appropriate with a tra file
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION "RUN"
STR_VAR
	"file" = ""
	"file_loc" = ""
	"tra" = ""
	"version" = ""
BEGIN
	ACTION_IF ("%file_loc%" STRING_EQUAL "") BEGIN
		OUTER_TEXT_SPRINT "filespec" "%mod_root%/%file%.tph"
	END ELSE BEGIN
		OUTER_TEXT_SPRINT "filespec" "%mod_root%/%file_loc%/%file%.tph"
	END
	ACTION_IF !(FILE_EXISTS "%filespec%") BEGIN
		FAIL ~You have tried to run "%file%", but "%filespec%" doesn't exist~
	END
	PRINT ~Including and running function "%file%"...~
	INCLUDE ~%filespec%~
	WITH_TRA "%tra_loc%/%base_language%/%tra%.tra" "%tra_loc%/%LANGUAGE%/%tra%.tra" BEGIN
		LAF ~%file%~
		STR_VAR
			"version"
		END
	END
END